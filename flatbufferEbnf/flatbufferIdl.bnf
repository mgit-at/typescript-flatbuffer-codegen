SCHEMA ::= INCLUDE* ( NAMESPACE_DECL | TABLE_DECL | STRUCT_DECL | ENUM_DECL | ROOT_DECL | FILE_EXTENSION_DECL | FILE_IDENTIFIER_DECL | ATTRIBUTE_DECL | RPC_DECL | OBJECT )*

SCHEMA_GENERATOR_SCHEMA ::= ATTRIBUTE_DECL | ENUM_DECL | TABLE_DECL | STRUCT_DECL

INCLUDE ::= 'include' STRING_CONSTANT ';'

NAMESPACE_DECL ::= 'namespace' IDENTIFIER ('.' IDENTIFIER)* ';'

TABLE_DECL ::= 'table' IDENTIFIER METADATA? '{' TABLE_FIELD_DECL* '}'
STRUCT_DECL ::= 'struct' IDENTIFIER METADATA? '{' STRUCT_FIELD_DECL+ '}'

ENUM_DECL ::= ('enum' IDENTIFIER ':' INT_TYPE | 'union' IDENTIFIER) METADATA? '{' ENUM_VAL_DECL* '}'

ROOT_DECL ::= 'root_type' IDENTIFIER ';'

FILE_EXTENSION_DECL ::= 'file_extension' STRING_CONSTANT ';'

FILE_IDENTIFIER_DECL ::= 'file_identifier' STRING_CONSTANT ';'

ATTRIBUTE_DECL ::= 'attribute' IDENTIFIER ';'

RPC_DECL ::= 'rpc_service' IDENTIFIER '{' RPC_METHOD* '}'

OBJECT ::= '{' IDENTIFIER ':' SINGLE_VALUE (',' IDENTIFIER ':' SINGLE_VALUE)* '}'

METADATA_IDENTIFIER ::= IDENTIFIER
IDENTIFIER ::= [a-zA-Z_][a-zA-Z0-9_]*

METADATA ::= '(' METADATA_IDENTIFIER ( ':' SINGLE_VALUE_NO_BOOL )? (',' METADATA_IDENTIFIER ( ':' SINGLE_VALUE_NO_BOOL )?)* ')'

TABLE_FIELD_DECL ::= IDENTIFIER ':' TYPE_IDENTIFIER ('=' SCALAR)? METADATA? ';'
STRUCT_FIELD_DECL ::= IDENTIFIER ':' STRUCT_TYPE_IDENTIFIER ('=' SCALAR)? METADATA? ';'
ENUM_VAL_DECL ::= IDENTIFIER ('=' INTEGER_CONSTANT)? ','

RPC_METHOD ::= IDENTIFIER '(' IDENTIFIER ')' ':' IDENTIFIER METADATA? ';'

SINGLE_VALUE_NO_BOOL ::= INTEGER_CONSTANT | FLOAT_CONSTANT | STRING_CONSTANT
SINGLE_VALUE ::= SCALAR | STRING_CONSTANT

SCALAR ::= BOOLEAN_CONSTANT | INTEGER_CONSTANT | FLOAT_CONSTANT

BOOLEAN_CONSTANT ::= 'true' | 'false'
INTEGER_CONSTANT ::= DECIMAL_INTEGER_CONSTANT | HEX_INTEGER_CONSTANT
FLOAT_CONSTANT ::= DECIMAL_INTEGER_CONSTANT | HEX_INTEGER_CONSTANT | SPECIAL_FLOAT_CONSTANT

DECIMAL_INTEGER_CONSTANT ::= PLUS_MINUS? DIGIT+
HEX_INTEGER_CONSTANT ::= PLUS_MINUS? '0' XDIGIT_X DIGIT+
SPECIAL_FLOAT_CONSTANT ::= PLUS_MINUS? ('nan'|'inf'|'infinity')

STRING_CONSTANT ::= '"' [^"]* '"' | "'" [^']* "'"
DIGIT ::= [0-9]
XDIGIT ::= [0-9a-fA-F]

XDIGIT_X ::= 'x' | 'X'
PLUS_MINUS ::= '+' | '-'

SIMPLE_INT_TYPE ::= 'byte' | 'ubyte' | 'short' | 'ushort' | 'int' | 'uint' | 'int8' | 'uint8' | 'int16' | 'uint16'|  'int32' | 'uint32'
LONG_INT_TYPE ::= 'long' | 'ulong' | 'int64' | 'uint64'
INT_TYPE ::= SIMPLE_INT_TYPE | LONG_INT_TYPE
FLOAT_TYPE ::= 'float' | 'double' | 'float32' | 'float64'
NUMBER_TYPE ::= INT_TYPE | FLOAT_TYPE
BOOL_TYPE ::= 'bool'
STRING_TYPE ::= 'string'

TYPE ::= NUMBER_TYPE | BOOL_TYPE | STRING_TYPE
TYPE_IDENTIFIER ::= TYPE | '[' TYPE ']' | IDENTIFIER

STRUCT_TYPE ::= BOOL_TYPE | SIMPLE_INT_TYPE
STRUCT_TYPE_IDENTIFIER ::= STRUCT_TYPE | '[' STRUCT_TYPE ']'

TS_FB_GEN_ATTRIBUTE ::= 'proxy' | 'uid' |
