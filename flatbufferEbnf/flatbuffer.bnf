SCHEMA ::= (ENUM_DECL NL) | (UNION_DECL NL) | (TABLE_DECL NL) | STRUCT_DECL

TABLE_DECL ::= 'table' WS GEN_TABLE_IDENTIFIER WS '{' NL (TAB TABLE_FIELD_DECL)* '}' NL
TABLE_FIELD_DECL ::= IDENTIFIER ':' WS (TYPE_WITH_SCALAR_AND_ATTRIBUTES | OTHER_TABLE_IDENTIFIER_WITH_ATTRIBUTES | OTHER_STRUCT_IDENTIFIER_WITH_ATTRIBUTES) ';' NL

STRUCT_DECL ::= 'struct' WS GEN_STRUCT_IDENTIFIER WS '{' NL (TAB STRUCT_FIELD_DECL)+ '}' NL
STRUCT_FIELD_DECL ::= IDENTIFIER ':' WS STRUCT_TYPE ';' NL

ENUM_DECL ::= 'enum' WS IDENTIFIER WS ':' WS INT_TYPE WS '{' NL TAB ENUM_VAL_NONE ((TAB ENUM_VAL_WITH_VALUE_DECL)* | (TAB ENUM_VAL_DECL)*) '}' NL
ENUM_VAL_DECL ::= IDENTIFIER ',' NL
ENUM_VAL_WITH_VALUE_DECL ::= IDENTIFIER WS '=' WS GEN_DIGIT ',' NL
ENUM_VAL_NONE ::= 'NONE' WS '=' WS '0' ',' NL

UNION_DECL ::= 'union' WS IDENTIFIER WS '{' NL (TAB UNION_VAL_DECL)* '}' NL
UNION_VAL_DECL ::= USE_TABLE_IDENTIFIER ',' NL

BOOLEAN_CONSTANT ::= 'true' | 'false'
INTEGER_CONSTANT ::= DECIMAL_INTEGER_CONSTANT_MINUS
INTEGER_CONSTANT_NO_MINUS ::= DECIMAL_INTEGER_CONSTANT
FLOAT_CONSTANT ::= DECIMAL_INTEGER_CONSTANT | SPECIAL_FLOAT_CONSTANT

DECIMAL_INTEGER_CONSTANT ::= DIGIT+
DECIMAL_INTEGER_CONSTANT_MINUS ::= MINUS? DECIMAL_INTEGER_CONSTANT
SPECIAL_FLOAT_CONSTANT ::= MINUS? ('nan'|'inf'|'infinity')

GEN_TABLE_IDENTIFIER ::= IDENTIFIER
USE_TABLE_IDENTIFIER ::= 'OTGEN'
OTHER_TABLE_IDENTIFIER ::= USE_TABLE_IDENTIFIER | ('[' USE_TABLE_IDENTIFIER ']')
OTHER_TABLE_IDENTIFIER_WITH_ATTRIBUTES ::= OTHER_TABLE_IDENTIFIER (WS PROXY_ATTRIBUTE)?

GEN_STRUCT_IDENTIFIER ::= IDENTIFIER
USE_STRUCT_IDENTIFIER ::= 'OSGEN'
OTHER_STRUCT_IDENTIFIER ::= USE_STRUCT_IDENTIFIER | ('[' USE_STRUCT_IDENTIFIER ']')
OTHER_STRUCT_IDENTIFIER_WITH_ATTRIBUTES ::= OTHER_STRUCT_IDENTIFIER (WS PROXY_ATTRIBUTE)?

IDENTIFIER ::= 'GEN'
DIGIT ::= '3'
GEN_DIGIT ::= '1337'
MINUS ::= '-'


UNSIGNED_SIMPLE_INT_TYPE ::= 'ubyte' | 'ushort' | 'uint' | 'uint8' | 'uint16' | 'uint32'
SIGNED_SIMPLE_INT_TYPE ::= 'byte' | 'short' | 'int' | 'int8' | 'int16'|  'int32'
SIMPLE_INT_TYPE::= UNSIGNED_SIMPLE_INT_TYPE | SIGNED_SIMPLE_INT_TYPE

UNSIGNED_SIMPLE_INT_TYPE_WITH_SCALAR ::= (UNSIGNED_SIMPLE_INT_TYPE (WS '=' WS INTEGER_CONSTANT_NO_MINUS)?) | ('[' UNSIGNED_SIMPLE_INT_TYPE ']')
SINGED_SIMPLE_INT_TYPE_WITH_SCALAR ::= (SIGNED_SIMPLE_INT_TYPE (WS '=' WS INTEGER_CONSTANT)?) | ('[' SIGNED_SIMPLE_INT_TYPE ']')

UNSIGNED_SIMPLE_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES ::= UNSIGNED_SIMPLE_INT_TYPE_WITH_SCALAR (WS (PROXY_ATTRIBUTE))? | UNSIGNED_SIMPLE_INT_TYPE (WS (UID_ATTRIBUTE))
SIGNED_SIMPLE_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES ::= SINGED_SIMPLE_INT_TYPE_WITH_SCALAR (WS (PROXY_ATTRIBUTE))?


UNSIGNED_LONG_INT_TYPE ::= 'ulong' | 'uint64'
SIGNED_LONG_INT_TYPE ::= 'long' | 'int64'
LONG_INT_TYPE ::= UNSIGNED_LONG_INT_TYPE | SIGNED_LONG_INT_TYPE

UNSIGNED_LONG_INT_TYPE_WITH_SCALAR ::= (UNSIGNED_LONG_INT_TYPE (WS '=' WS INTEGER_CONSTANT_NO_MINUS)?) | ('[' UNSIGNED_LONG_INT_TYPE ']')
SIGNED_LONG_INT_TYPE_WITH_SCALAR ::= (SIGNED_LONG_INT_TYPE (WS '=' WS INTEGER_CONSTANT)?) | ('[' SIGNED_LONG_INT_TYPE ']')

UNSIGNED_LONG_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES ::= UNSIGNED_LONG_INT_TYPE_WITH_SCALAR LONG_ATTRIBUTES? | UNSIGNED_LONG_INT_TYPE (UID_ATTRIBUTE | UID_AND_PRESERVE_LONG_ATTRIBUTE)
SIGNED_LONG_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES ::= SIGNED_LONG_INT_TYPE_WITH_SCALAR LONG_ATTRIBUTES?


UNSIGNED_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES ::= UNSIGNED_SIMPLE_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES | UNSIGNED_LONG_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES
SIGNED_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES ::= SIGNED_SIMPLE_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES | SIGNED_LONG_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES
INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES ::= UNSIGNED_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES | SIGNED_INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES

INT_TYPE ::= SIMPLE_INT_TYPE | LONG_INT_TYPE


FLOAT_TYPE ::= 'float' | 'double' | 'float32' | 'float64'

FLOAT_TYPE_WITH_SCALAR ::= (FLOAT_TYPE (WS '=' WS FLOAT_CONSTANT)?) | ('[' FLOAT_TYPE ']')


BOOL_TYPE ::= 'bool'

BOOL_TYPE_WITH_SCALAR ::= (BOOL_TYPE (WS '=' WS BOOLEAN_CONSTANT)?) | ('[' BOOL_TYPE ']')


STRING_TYPE ::= 'string'

STRING_TYPE_WITH_SCALAR ::= STRING_TYPE | ('[' STRING_TYPE ']')


STRUCT_TYPE ::= BOOL_TYPE | SIMPLE_INT_TYPE

TYPE_WITH_SCALAR_AND_ATTRIBUTES ::= INT_TYPE_WITH_SCALAR_AND_ATTRIBUTES | ((BOOL_TYPE_WITH_SCALAR | FLOAT_TYPE_WITH_SCALAR | STRING_TYPE_WITH_SCALAR) (WS PROXY_ATTRIBUTE)?)


UID_ATTRIBUTE ::= '(uid)'
PRESERVE_LONG_ATTRIBUTE  ::= '(preserveLong)'
UID_AND_PRESERVE_LONG_ATTRIBUTE ::= '(uid, preserveLong)'
PROXY_ATTRIBUTE ::= '(proxy' ':6'? ')'
PROXY_AND_PRESERVE_LONG_ATTRIBUTE ::= '(preserveLong, proxy' ':6'? ')'

LONG_ATTRIBUTES ::= (WS (PROXY_ATTRIBUTE | PRESERVE_LONG_ATTRIBUTE | PROXY_AND_PRESERVE_LONG_ATTRIBUTE))

WS ::= ' '
NL ::= '\n'
TAB ::= '\t'
